// Module :mobile
plugins {
    id "com.android.application"
    id "com.google.firebase.appdistribution"
    id "com.google.firebase.crashlytics"
}

android {

    defaultConfig {
        applicationId 'io.syslogic.cloudbuild'
        namespace 'io.syslogic.cloudbuild'
        multiDexEnabled true
        targetSdkVersion 33
        compileSdkVersion 33
        minSdkVersion 22
        versionName "1.0.1"
        versionCode 2
    }

    lint {
        abortOnError false
    }

    if (rootProject.file('keystore.properties').exists()) {
        signingConfigs {
            debug {
                storeFile rootProject.file(".android${File.separator}debug.keystore")
                storePassword rootProject.ext.get('debugKeystorePass')
                keyAlias rootProject.ext.get('debugKeyAlias')
                keyPassword rootProject.ext.get('debugKeyPass')
            }
            release {
                storeFile rootProject.file(".android${File.separator}release.keystore")
                storePassword rootProject.ext.get('releaseKeystorePass')
                keyAlias rootProject.ext.get('releaseKeyAlias')
                keyPassword rootProject.ext.get('releaseKeyPass')
            }
        }

        buildTypes {

            debug {
                signingConfig signingConfigs.debug
            }

            release {
                signingConfig signingConfigs.release

                firebaseAppDistribution {
                    serviceCredentialsFile = rootProject.file("credentials/google-service-account.json")
                    releaseNotesFile = rootProject.file("credentials/release_notes.txt")
                    testersFile = rootProject.file("credentials/testers.txt")
                }

                firebaseCrashlytics {
                    nativeSymbolUploadEnabled false
                    unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib/"
                    strippedNativeLibsDir "build/intermediates/stripped_native_libs/release/out/lib/"
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    implementation 'com.google.firebase:firebase-crashlytics:18.3.6'
    // implementation 'com.google.firebase:firebase-crashlytics-ndk:18.3.6'
}

if (project.file('google-services.json').exists()) {
    apply plugin: "com.google.gms.google-services"
} else {
    logger.error('> File google-services.json is missing.')
}
